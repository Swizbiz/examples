plugins {
    id 'org.gretty' version '2.3.1'
    id 'com.devsoap.vaadin-flow' version '1.0.0.RC7'
}

group = 'com.vaadin.starter'
version = '1.0-SNAPSHOT'

description = """Beverage Buddy App Starter for Vaadin Flow"""

sourceCompatibility = 1.8
targetCompatibility = 1.8

vaadin {
    // Vaadin version to use
    version = '12.0.0'

    // Enable production mode for production apps
    //productionMode = true
}

// We specify here manually the dependencies, another option would be to just autoconfigure and remove the
// repositories and dependencies sections
//vaadin.autoconfigure()

repositories {

    // Include JCenter to fetch dependencies from
    jcenter()

    // Include all necessery Vaadin repositories
    vaadin.repositories()
}

dependencies {
    // Include BOM to stabilize Vaadin versions
    compile         vaadin.bom()
    // Include only open source Vaadin components
    compile(vaadin.core())
    // Include a compatible servlet API version
    compileOnly     vaadin.servletApi()
    // Include a compatible slf4j version
    runtime         vaadin.slf4j()
}


/*
 * By default the vaadin.core() dependency will import both Material and Lumo themes. This project
 * uses the Lumo theme so we want exlcude the Material theme.
 *
 * To do that we need to do the following:
 */

// In production mode we want to exclude the Material styles imported from all vaadin components
vaadinTranspileDependencies {
    importExcludes = [
        '.*/theme/material/.*',
        '.*/vaadin-material-styles/.*'
    ]
}

// We don't want the Material style Maven dependencies in our project either
configurations.all {
    exclude group: 'com.vaadin', module: 'vaadin-material-theme'
    exclude group: 'org.webjars.bowergithub.vaadin', module: 'vaadin-material-styles'
}

